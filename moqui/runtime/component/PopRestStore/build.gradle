/*
 * This software is in the public domain under CC0 1.0 Universal plus a
 * Grant of Patent License.
 *
 * To the extent possible under law, the author(s) have dedicated all
 * copyright and related and neighboring rights to this software to the
 * public domain worldwide. This software is distributed without any
 * warranty.
 *
 * You should have received a copy of the CC0 Public Domain Dedication
 * along with this software (see the LICENSE.md file). If not, see
 * <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

import java.nio.charset.StandardCharsets

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.gradle-webtools.minify:gradle-minify-plugin:1.3.1"
    }
}
apply plugin: "org.gradlewebtools.minify"

def compsPath = "screen/store/components"
// source JS files in load order
def compsJsFiles = [compsPath + "/utilities.js", compsPath + "/ApiServices.js", compsPath + "/ComponentsNav.js",
        compsPath + "/ComponentsProduct.js", compsPath + "/ComponentsAccount.js", compsPath + "/ComponentsCheckout.js", compsPath + "/main.js"]

task combineBaseJs {
    doLast {
        def destFile = file("${compsPath}/combined.js")
        destFile.write("", "UTF-8")
        compsJsFiles.each({
            destFile.append(file(it).getText("UTF-8"), "UTF-8")
            destFile.append("\n", "UTF-8")
        })
    }
}
task minifyCombined(type: org.gradlewebtools.minify.JsMinifyTask) {
    dependsOn combineBaseJs

    // see: https://github.com/gradle-webtools/gradle-minify-plugin
    // NOTE: JsMinifyTask (and the css one, etc) always recursively minify in sub-directories, no way to disable! (based on source review 2022-04-05)
    srcDir = file(compsPath)
    dstDir = file(compsPath)
    options.ignoreMinFiles = true
    options.emitUseStrict = false
    options.strictModeInput = false
    options.compilationLevel = com.google.javascript.jscomp.CompilationLevel.SIMPLE_OPTIMIZATIONS
    options.env = com.google.javascript.jscomp.CompilerOptions.Environment.BROWSER
    options.warningLevel = com.google.javascript.jscomp.WarningLevel.QUIET
    options.charset = StandardCharsets.UTF_8
    // options.outputCharset = StandardCharsets.UTF_8
    options.setLanguageIn(com.google.javascript.jscomp.CompilerOptions.LanguageMode.fromString("ECMASCRIPT6"))
    options.setLanguageOut(com.google.javascript.jscomp.CompilerOptions.LanguageMode.fromString("ES5"))
}

task clean { doLast {
    delete fileTree(dir: compsPath, include: 'combined.*')
    delete fileTree(dir: compsPath, include: '*.min.js')
}}
task build {
    dependsOn minifyCombined
}
